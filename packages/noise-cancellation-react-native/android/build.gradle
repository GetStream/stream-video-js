buildscript {
    ext.getExtOrDefault = { name -> return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties['NoiseCancellationReactNative_' + name]
    }

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath("com.android.tools.build:gradle:7.3.1")
        // noinspection DifferentKotlinGradleVersion
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${getExtOrDefault('kotlinVersion')}"
    }
}


apply plugin: "com.android.library"
apply plugin: "kotlin-android"


def getExtOrIntegerDefault(name) {
    return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["NoiseCancellationReactNative_" + name]).toInteger()
}

def supportsNamespace() {
    def parsed = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION.tokenize('.')
    def major = parsed[0].toInteger()
    def minor = parsed[1].toInteger()

    // Namespace support was added in 7.3.0
    return (major == 7 && minor >= 3) || major >= 8
}

android {
    if (supportsNamespace()) {
        namespace "io.getstream.rn.noisecancellation"

        sourceSets {
            main {
                manifest.srcFile "src/main/AndroidManifestNew.xml"
            }
        }
    }

    compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

    defaultConfig {
        minSdkVersion getExtOrIntegerDefault("minSdkVersion")
        targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    lintOptions {
        disable "GradleCompatible"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

rootProject.allprojects {
    repositories {
        maven {
            name = 'Central Portal Snapshots'
            url = 'https://central.sonatype.com/repository/maven-snapshots/'
            content {
                // This efficiently tells Gradle to only look for this specific dependency in this repository
                includeModule('io.getstream', 'stream-video-android-noise-cancellation')
            }
        }
    }
}

repositories {
    mavenCentral()
    google()
}

def kotlin_version = getExtOrDefault("kotlinVersion")

println "Building Noise Cancellation React Native module with Kotlin version: $kotlin_version"
println "Using compileSdkVersion: ${getExtOrIntegerDefault("compileSdkVersion")}"
println "Using minSdkVersion: ${getExtOrIntegerDefault("minSdkVersion")}"
println "Using targetSdkVersion: ${getExtOrIntegerDefault("targetSdkVersion")}"


dependencies {
    implementation "com.facebook.react:react-native:+"
    implementation 'io.getstream:stream-video-android-noise-cancellation:1.0.4-kotlin-1.9.25'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation project(':stream-io_react-native-webrtc')
}

